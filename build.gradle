plugins {
  id "maven-publish"
  id "com.github.node-gradle.node" version "7.1.0"
  id "com.enonic.xp.app" version "3.6.1"
}

app {
  name = "${appName}"
  displayName = "${appDisplayName}"
  vendorName = "${vendorName}"
  vendorUrl = "${vendorUrl}"
  systemVersion = "${xpVersion}"
}

dependencies {
  implementation "com.enonic.xp:core-api:${xpVersion}"
  implementation "com.enonic.xp:portal-api:${xpVersion}"
  include "no.api.freemarker:freemarker-java8:3.0.3"
  include "no.item:lib-xp-freemarker:3.0.0-SNAPSHOT"
  include "no.item:lib-xp-time:1.2.1"
  include "com.enonic.lib:lib-thymeleaf:2.0.1"
}

repositories {
  mavenCentral()
  xp.enonicRepo()
  maven { url "https://repo.itemtest.no/releases" }
  maven { url "https://repo.itemtest.no/snapshots" }
}

node {
  download = true
  version = "24.3.0"
}

processResources {
	exclude "**/.gitkeep"
  exclude "**/*.json"
	exclude "**/*.ts"
	exclude "**/*.tsx"
}

tasks.register("dev", Exec) {
  if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
    commandLine "cmd", "/c", "gradlew.bat", "deploy", "-t"
  } else {
    commandLine "./gradlew", "deploy", "-t"
  }
}

tasks.register("npmBuild", NpmTask) {
  args = [
    "run",
    "--silent",
    "build"
  ]
  dependsOn npmInstall
  environment = [
    "FORCE_COLOR": "true",
    "LOG_LEVEL_FROM_GRADLE": gradle.startParameter.logLevel.toString(),
    "NODE_ENV": project.hasProperty("dev") || project.hasProperty("development") ? "development" : "production"
  ]
  inputs.dir "src/main/resources"
  outputs.dir "build/resources/main"
  outputs.upToDateWhen { false }
}

jar.dependsOn npmBuild

tasks.register("npmCheck", NpmTask) {
	dependsOn npmInstall
	args = [
		"run",
		"check"
	]
	environment = [
		"FORCE_COLOR": "true",
	]
}

check.dependsOn npmCheck

tasks.withType(Copy).configureEach {
  includeEmptyDirs = false
}

publishing {
  repositories {
    maven {
      name = "itemtestRepository"
      url = version.endsWith('SNAPSHOT')
        ? "https://repo.itemtest.no/snapshots"
        : "https://repo.itemtest.no/releases"
      credentials(PasswordCredentials)
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from components.java
      groupId group
      artifactId projectName
      version version
    }
  }
}
